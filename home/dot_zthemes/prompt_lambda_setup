# vim: ft=zsh
pmodload 'helper'

function prompt_lambda_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_lambda_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_lambda_precmd

  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    colors=(
      "%F{67}"  # 1 blue
      "%F{229}" # 2 yellow
      "%F{210}" # 3 magenta
      "%F{131}" # 4 red
      "%F{108}" # 5 green
      "%F{208}" # 6 orange
      "%F{60}"  # 7 violet
      "%F{242}" # 8 grey
    )
  else
    colors=(
      "%F{blue}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{orange}"
      "%F{violet}"
      "%F{grey}"
    )
  fi

  # git ●⇡⇣⬆⬇
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "%B${colors[7]}%b%f"
  zstyle ':prezto:module:git:info:commit' format ":${colors[8]}%.7c%f "
  zstyle ':prezto:module:git:info:ahead' format "${colors[1]}⇡%f"
  zstyle ':prezto:module:git:info:behind' format "${colors[1]}⇣%f"
  zstyle ':prezto:module:git:info:added' format "${colors[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${colors[4]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${colors[6]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${colors[1]}●%f"
  zstyle ':prezto:module:git:info:stashed' format "${colors[3]}$%f"
  zstyle ':prezto:module:git:info:keys' format 'rprompt' '%b%c%A%B%d%a%m%u%S'

  # python
  zstyle ':prezto:module:python:info:virtualenv' format "${colors[1]}[%v]%f"

  # prompt
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B%(?;%{${colors[1]}%};%{${colors[4]}%})λ%f%b"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "%B${colors[8]}❯%f"

  # PROMPT="${colors[1]}%f "
  PROMPT='${editor_info[keymap]} '
  RPROMPT="${colors[1]}%2~%f "'$git_info[rprompt]'" "'$python_info[virtualenv]'" "'$(kube_ps1)'""
}

prompt_lambda_setup "$@"
